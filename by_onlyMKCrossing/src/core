//[0-1_備忘錄]
//
//[1-1_傳入函數之參數]
input: _stdevSTAT(numericSimple);
input: _stdevUNSTAT(numericSimple);
input: _stdevUUNSTAT(numericSimple);

input: _marketPrice_avg_RAW(numericseries);         //line for trend
input: _marketPrice_avg_addone_RAW(numericseries);  //line for CRP

input: _folio_POS_Line(numericREF);     //line for CRP
input: _folio_POS_LineCLP(numericREF);  //line for CLP
input: _folio_POS_LineSTP(numericREF);  //line for STP

input: _qc_avg(numericREF);
input: _qc_stdw(numericREF);

input: _DEBUG_INFO(stringRef);

//[1-2_檢查傳入之核心參數]

//[1-3_local參數建立與重置]
array: _qc_array[](0);
variable: _len(0);
_len =intPortion(4.5*60/barInterval)*5; //for 早盤K棒數1week
array_SetMaxIndex(_qc_array, _len);

//[1-4_特定檢查項目]
////早開盤K檢查項目
variable: _myOPEND(0);
variable: _avg_count(0);
variable: _mkopd(0);
variable: _avg_mkopd(0);
variable: _marketPrice_avg_preWeekily_RAW(0);

if time =084500 then
begin
    //早盤1stK時歸零
    _myOPEND =open;
    _avg_count =0;
    _mkopd =0;

    if openweekly <>openweekly[1] then //新週第一天早盤開盤K
        _marketPrice_avg_preWeekily_RAW =_marketPrice_avg_RAW[1];   

end else
begin
    _avg_count =_avg_count +1;
    _mkopd =_mkopd +(_marketPrice_avg_addone_RAW -_myOPEND);
    _avg_mkopd =_mkopd /_avg_count;
end;

////設定策略使用之時段(早盤)
variable: _time_sec(FALSE);
_time_sec =time >084500
			and time <150000;

//[2-1_策略主體]
////收集特定時段內漲跌資料
variable: _qc_diff(0);
variable: _qc_counter(0);
if currentbar >1 
    and _time_sec then
begin
    //save diffvalue into arrayREF
    _qc_diff =iff(close[1] -open[1] >0, 
                    high[1] -open[1],
                    low[1]  -open[1]);
    reflash_Data("_Push_", _qc_array, _qc_diff);
	if _qc_counter <_len then _qc_counter +=1;
end;

////計算漲跌資料標差
variable: _qc_sum(0);
variable: _qc_diffq_sum(0);

variable: _range_L(0);
variable: _range_S(0);
variable: _range_LL(0);
variable: _range_SS(0);

variable: _folio_base(10);

variable: _LS_ratio(0);
variable: _LS_mk_ratio(0);
variable: _switch_L(FALSE);
variable: _switch_S(FALSE);
variable: _switch_DL(FALSE);
variable: _switch_DS(FALSE);
variable: _switch_mk_L(FALSE);
variable: _switch_mk_S(FALSE);
variable: _switch_opd_L(FALSE);
variable: _switch_opd_S(FALSE);
variable: intraBarPersist _time_CRP(0);

_qc_sum      =0;
_qc_diffq_sum=0;

if _qc_counter >=_len then //完成採樣數
begin
    
	////計算標準差
    //////計算總合
    for value1 =1 to _len
    begin
        _qc_sum =_qc_sum +_qc_array[value1];
    end;
    //////計算平均
    _qc_avg =_qc_sum /_len;
    //////計算變異數
    for value1 =1 to _len
    begin
        _qc_diffq_sum =_qc_diffq_sum +square(_qc_array[value1] -_qc_avg);
    end;
    //////計算標準差
    _qc_stdw = squareRoot(_qc_diffq_sum/_len);

    //////計算趨勢向量
    _LS_ratio   =iff(_marketPrice_avg_preWeekily_RAW =0,
                        0,
                        _marketPrice_avg_preWeekily_RAW -_marketPrice_avg_addone_RAW); //用於週1st早K

    _LS_mk_ratio=_marketPrice_avg_RAW -_marketPrice_avg_addone_RAW;
    //////計算機率區間漲跌幅
    _range_L  =_qc_avg +_qc_stdw*_stdevSTAT;
    _range_LL =_qc_avg +_qc_stdw*_stdevUNSTAT;
    _range_S  =_qc_avg -_qc_stdw*_stdevSTAT;
    _range_SS =_qc_avg -_qc_stdw*_stdevUNSTAT;

	//[2-1_產生盤勢分析]
    _switch_L       =FALSE;
    _switch_S       =FALSE;
    _switch_DL      =FALSE;
    _switch_DS      =FALSE;
    _switch_mk_L    =FALSE;    
    _switch_mk_S    =FALSE;
    _switch_opd_L   =FALSE;
    _switch_opd_L   =FALSE;

    //當雙MK線重合
    if _marketPrice_avg_RAW =_marketPrice_avg_addone_RAW then
    begin
        //K標差漲幅>基本利數
        if _range_L >_folio_base 
			{and _LS_ratio >0} then
        begin
            //多穿越型
            _switch_L =_marketPrice_avg_addone_RAW -open <_range_L 
                        and _LS_ratio >0
                        and open <_marketPrice_avg_addone_RAW
                        and high >=_marketPrice_avg_addone_RAW;
            //多急漲型
            _switch_DL=high[1] -open <_range_L
                        and high[1] -open >0
                        and high >=high[1]
                        and _avg_mkopd >0
                        and time[1] <>084500;
        end;
        //K標差跌幅>基本利數(以下因計算負數所以*-1並反寫)
        if _range_S <_folio_base*-1 
			{and _LS_ratio <0} then
        begin
            //空穿越型
            _switch_S =_marketPrice_avg_addone_RAW -open >_range_S
                        and _LS_ratio <0
                        and open >_marketPrice_avg_addone_RAW 
                        and low <=_marketPrice_avg_addone_RAW;
            //空急跌型
            _switch_DS=low[1] -open >_range_S
                        and low[1] -open <0
                        and low <=low[1]
                        and _avg_mkopd <0
                        and time[1] <>084500;
        end;
    end else
    begin
        //當雙MK線分開
        //K標差漲幅>基本利數
        if _range_L >_folio_base 
            and _LS_mk_ratio >0 then
        begin
            //多穿越型
            _switch_mk_L =_marketPrice_avg_addone_RAW -open <_range_L
                                and open <_marketPrice_avg_addone_RAW 
                                and high >=_marketPrice_avg_addone_RAW;
            //多急漲型
            _switch_opd_L =high[1] -open <_range_L
                                and open <high[1]
                                and high >=high[1]
                            and _avg_mkopd >0
                            and time[1] <>084500;
        end;
        //K標差跌幅>基本利數(以下因計算負數所以*-1並反寫)
        if _range_S <_folio_base*-1 
            and _LS_mk_ratio <0 then
        begin
            //空穿越型
            _switch_mk_S =_marketPrice_avg_addone_RAW -open >_range_S
                            and open >_marketPrice_avg_addone_RAW
                            and low <=_marketPrice_avg_addone_RAW;
            //空急跌型
            _switch_opd_S =low[1] -open >_range_S
                                and open >low[1]
                                and low <=low[1]
                            and _avg_mkopd <0
                            and time[1] <>084500;
        end;
    end;

	//[3-1_產生各進出場線]
    //當位於策略生效時段
	if _time_sec then
	begin

        //雙MK重合
        ////穿越型
		if _switch_L
			or _switch_S then
		begin
			_folio_POS_Line =p_n(_LS_ratio) *_marketPrice_avg_addone_RAW;
		end else
        ////急漲跌型
        if _switch_DL
            or _switch_DS then
        begin
            _folio_POS_Line =iff(_switch_DL, high[1], low[1]) *iff(_switch_DL, 1,-1);
        end else

        //雙MK分開
        ////穿越型
        if _switch_mk_L
            or _switch_mk_S then
        begin
            _folio_POS_Line =p_n(_LS_mk_ratio) *_marketPrice_avg_addone_RAW;
        end else 
        ////急漲跌型
        if _switch_opd_L
            or _switch_opd_S then
        begin
            _folio_POS_Line =iff(_switch_opd_L, high[1], low[1]) *iff(_switch_opd_L, 1,-1);
        end;

        //產生出場線_folio_POS_LineCLP
        ////當沖型高標差出場法
		_folio_POS_LineCLP =absValue(iff(_folio_POS_Line >0, 
                                        maxlist(_range_LL, _folio_base), 
									    minlist(_range_SS, _folio_base*-1)))
                                    *p_n(_folio_POS_Line) 
							+absValue(_folio_POS_Line);

        //產生停損線_folio_POS_LineSTP
        ////如產生新的進場線
		variable: _stp_line(0);
{		if _folio_POS_Line <>_folio_POS_Line[1] then
		begin
			_stp_line =open +iff(_folio_POS_Line >0, _range_S, _range_L);
		end;}
		
		if _folio_POS_Line <>_folio_POS_Line[1] then 
		begin
			_stp_line =open +iff(_folio_POS_Line >0, _range_S, _range_L);
			_folio_POS_LineSTP =iff(_folio_POS_Line >0, minList(low[1], _stp_line), maxList(high[1], _stp_line));
		end;
	end;

end;

_DEBUG_INFO =text(

		 _qc_avg, ",",
		 _qc_stdw, ",",
		 _range_L, ",",
		 _range_LL, ",",
		 _range_S, ",",
		 _range_SS, ",",

		 _LS_ratio, ",",
		 _LS_mk_ratio, ",",

		 _switch_L, ",",
		 _switch_S, ",",
		 _switch_DL, ",",
		 _switch_DS, ",",
		 _switch_mk_L, ",",
		 _switch_mk_S, ",",
		 _switch_opd_L, ",",
		 _switch_opd_S
		 
);




