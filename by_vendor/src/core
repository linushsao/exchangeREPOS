//[0-1_備忘錄]
//
//[1-1_傳入函數之參數]
input: _stdevSTAT(numericSimple);
input: _stdevUNSTAT(numericSimple);
input: _stdevUUNSTAT(numericSimple);

input: _stdMID(numericSeries);
input: _U2_STAT(numericSeries);
input: _D2_STAT(numericSeries);

input: _marketPrice_avg_RAW(numericseries);         //line for trend
input: _marketPrice_avg_addone_RAW(numericseries);  //line for CRP

input: _qc_ext_avg(numericseries);
input: _qc_ext_stdw(numericSeries);

input: _folio_POS_Line(numericREF);     //line for CRP
input: _folio_POS_LineRE(numericREF);   //line for CRPRe
input: _folio_POS_LineCLP(numericREF);  //line for CLP
input: _folio_POS_LineSTP(numericREF);  //line for STP

input: _DEBUG_INFO(stringRef);

//[1-2_檢查傳入之核心參數]

//[1-3_//通用參數建立與重置]
////設定策略使用之時段(早盤)
////TRUE表不設限
variable: _time_sec(FALSE);
_time_sec =TRUE;

//[1-4_特定檢查項目]
////早開盤K檢查項目

//[2-1_策略主體]
////策略參數設置與初始化
variable: _mk_CLP(0);
variable: _switch_L(FALSE);
variable: _switch_LD(FALSE);
variable: _switch_S(FALSE);
variable: _switch_SD(FALSE);
variable: _range_L(0);
variable: _range_S(0);
variable: _range_LL(0);
variable: _range_SS(0);

////計算機率區間漲跌幅
_range_L  =_qc_ext_avg +_qc_ext_stdw*_stdevSTAT;
_range_LL =_qc_ext_avg +_qc_ext_stdw*_stdevUNSTAT;
_range_S  =_qc_ext_avg -_qc_ext_stdw*_stdevSTAT;
_range_SS =_qc_ext_avg -_qc_ext_stdw*_stdevUNSTAT;

////計算趨勢向量
variable: _LS_ratio(0);
variable: _LS_addone_ratio(0);
variable: _mk_diff(0);
_LS_ratio		=_marketPrice_avg_RAW -_stdMID;
_LS_addone_ratio=_marketPrice_avg_addone_RAW -_stdMID;
_mk_diff 		=_marketPrice_avg_addone_RAW -_marketPrice_avg_RAW;

////往多方
_switch_L =_LS_ratio >0
			and open <_marketPrice_avg_RAW
			and high >=_marketPrice_avg_RAW
			and time[1] <>084500;
_switch_LD =_mk_diff <>0
			and _LS_addone_ratio >0
			and open <_marketPrice_avg_addone_RAW
			and high >=_marketPrice_avg_addone_RAW
			and time[1] <>084500;

////往空方
_switch_S =_LS_ratio <0
			and open >_marketPrice_avg_RAW 
			and low <=_marketPrice_avg_RAW
			and time[1] <>084500;
_switch_SD =_mk_diff <>0
			and _LS_addone_ratio <0
			and open >_marketPrice_avg_addone_RAW 
			and low <=_marketPrice_avg_addone_RAW
			and time[1] <>084500;

//[3-1_依策略運算產生各進出場線]
////當位於策略生效時段
if _time_sec then
begin

    ////產生主進場線
	if _switch_L
		or _switch_S then
	begin
		_folio_POS_Line =p_n(_LS_ratio) *_marketPrice_avg_RAW;
	end; 
	if _switch_LD
		or _switch_SD then
	begin
		_folio_POS_Line =p_n(_LS_addone_ratio) *_marketPrice_avg_addone_RAW;
	end;
    ////Kn
    if _folio_POS_Line =_folio_POS_Line[1] then
    begin
        ////產生出場線_folio_POS_LineCLP
        _mk_CLP =iff(_folio_POS_Line >0,
                        _U2_STAT,
                        _D2_STAT);

        if (_folio_POS_Line >0 
                and open >_mk_CLP 
                and low <_mk_CLP) 
            or 
            (_folio_POS_Line <0 
                and open <_mk_CLP 
                and high >_mk_CLP) then
        begin
            _folio_POS_LineCLP =_mk_CLP;
        end;

        ////產生平進循環線_folio_POS_LineRe
        if (_folio_POS_Line >0 
                and open >_marketPrice_avg_RAW
                and low <=_marketPrice_avg_RAW) 
            or 
            (_folio_POS_Line <0
                and open <_marketPrice_avg_RAW
                and high >=_marketPrice_avg_RAW) then
        begin
            _folio_POS_LineRE =iff(_folio_POS_Line >0,
                                high[1],
                                low[1]);
        end;

    end;

    //產生停損線_folio_POS_LineSTP
	variable: _stp_line(0);
    ////如產生新的進場線
	if _folio_POS_Line <>_folio_POS_Line[1] then 
	begin
		_stp_line =open +iff(_folio_POS_Line >0, _range_S, _range_L);
		_folio_POS_LineSTP =iff(_folio_POS_Line >0, minList(low[1], _stp_line), maxList(high[1], _stp_line));
	end;
end;

////
_DEBUG_INFO =text(

		 _qc_ext_avg, ",",
		 _qc_ext_stdw, ",",
		 _range_L, ",",
		 _range_LL, ",",
		 _range_S, ",",
		 _range_SS, ",",

		 _LS_ratio, ",",

		 _switch_L, ",",
		 _switch_S
		 
);




